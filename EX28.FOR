C*************************   ABSTRACT   ******************************
C
C      THIS PROGRAM CALCULATES THE OVERALL RECOVERY OBTAINED IN A COUNTER-
C   CURRENT EXTRACTION UNIT.  BY USING MATERIAL BALANCES AND THE EQUI-
C   LIBRIUM RELATIONSHIP, A SYSTEM OF LINEAR EQUATIONS WHICH HAVE A
C   TRIDIAGONAL COEFICIENT MATRIX RESULTS.  THE THOMAS METHOD IS APPLIED
C   TO THE SOLUTION OF THIS SYSTEM OF LINEAR EQUATIONS.
C
C
C***********************   NOMENCLATURE   ******************************
C
C   B(I)-  B IN EQUATION 2.8 WHERE I INDICATES THE EQUATION NUMBER
C   BETA(I)-  DEFINED BY EQUATIONS 2.11 AND 2.13
C   C(I)-  COEFFICIENT TO THE LEFT OF THE DIAGONAL (SEE EQ 2.8)
C   D(I)-  COEFFICIENT ON THE DIAGONAL (SEE EQ 2.8)
C   E(I)-  COEFFICIENT TO THE RIGHT OF THE DIAGONAL (SEE EQ 2.8)
C   GAM(I)-  DEFINED BY EQUATIONS 2.12 AND 2.14
C   K- DISTRIBUTION COEFFICIENT (DIMENSIONLESS)
C   N-  NUMBER OF STAGES IN THE LIQUID/LIQUID EXTRACTION UNIT
C   S-  FLOW RATE OF THE SOLVENT (KG/HR)
C   W-  FLOW RATE OF THE RAFFINATE (KG/HR)
C   X(I)-  THE WEIGHT FRACTION OF THE A IN W OF THE ITH STAGE
C   XIN-  THE WEIGHT FRACTION OF A IN W ENTERING THE FIRST STAGE
C   YIN-  THE WEIGHT FRACTION OF A IN S ENTERING THE LAST STAGE
C
C
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 K
      DIMENSION C(10),D(10),E(10),B(10),X(10),BETA(10),GAM(10)
C
C*********************   SET PHYSICAL PARAMETERS OF THE SYSTEM
C
      W=2000.
      S=1000.
      XIN=.05
      YIN=0.0
      N=10
      K=10.
C
C*******************   CALCULATE CONSTANTS FOR THE LINEAR EQUATIONS
C
      DO 10 I=1,N
      C(I)=1
      E(I)=K*S/W
      D(I)=-(K*S/W+1.)
   10 B(I)=0.0
      B(1)=-XIN
      B(N)=-YIN*K*S/W
C
C***********   CALL SUBROUTINE THAT IMPLEMENTS THE THOMAS METHOD
C
      CALL TM(N,C,D,E,B,X,BETA,GAM)
C
C********************   PRINT OUT THE RESULTS
C
      WRITE(6,16)
      WRITE(6,25)
   25 FORMAT( 10X,50H WEIGHT FRACTION A IN RAFFINATE OF EACH STAGE I   )
      DO 11 I=1,N
   11 WRITE(6,12)I,X(I)
   12 FORMAT( 10X,3H I=,I4,5X,3H X=,D14.7)
      RECOV=100.*(X(1)*K-YIN)*S/W/XIN
      WRITE(6,16)
   16 FORMAT( ///)
      WRITE(6,17)RECOV
   17 FORMAT( 10X,26H PERCENT RECOVERY OF A IS ,D14.7)
      WRITE(6,16)
      STOP
      END
C
C************************   ABSTRACT   *****************************
C
C      THIS SUBROUTINE CALCULATES THE SOLUTION OF A SYSTEM OF LINEAR
C   EQUATIONS WHICH HAVE A TRIDIAGONAL COEFICIENT MATRIX USING THE
C   THE THOMAS METHOD
C
C************************************************************************
C
      SUBROUTINE TM(N,C,D,E,B,X,BETA,GAM)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION C(1),D(1),E(1),B(1),X(1),BETA(1),GAM(1)
      BETA(1)=D(1)
      GAM(1)=B(1)/BETA(1)
      DO 10 I=2,N
      BETA(I)=D(I)-C(I)*E(I-1)/BETA(I-1)
   10 GAM(I)=(B(I)-C(I)*GAM(I-1))/BETA(I)
      X(N)=GAM(N)
      DO 20 I=2,N
      J=N-I+1
   20 X(J)=GAM(J)-E(J)*X(J+1)/BETA(J)
      RETURN
      END
