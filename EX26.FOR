******************************  ABSTRACT  *******************************
C
C     THIS PROGRAM SOLVES A SET OF LINEAR EQUATIONS USING THE GUASS-
C  SEIDEL METHOD.  THE GAUSS-SEIDEL METHOD IS AN ITERATIVE METHOD
C  WHICH WILL CONVERGE WHEN THE COEFICIENT MATRIX IS DIAGONALLY
C  DOMINANT.
C
C***************************   NOMENCALTURE  ****************************
C
C  A(I,J)-  THE COEFFICIENT OF THE JTH VARIABLE IN THE ITH EQUATION
C  B(I)-  THE CONSTANT TERM IN THE ITH EQUATION
C  ERLIM- THE ERROR CRITERIA
C  ITMAX- THE MAXIMUM NUMBER OF ITERATIONS ALLOWED
C  N-  THE NUMBER OF INDEPENDENT EQUATIONS
C  X(I)- THE VECTOR CONTAINING THE SOLUTION TO THE SET OF EQUATIONS
C
C************************************************************************
C
C
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION A(6,6),B(6),X(6)
C
C  INPUT THE DATA FOR THE PROBLEM
C
      N=6
C  ZERO THE COEFICIENT MATRIX
      DO 1 I=1,N
      DO 2 J=1,N
    2 A(I,J)=0.0
    1 CONTINUE
C  SET THE NONZERO ELEMENTS OF A(I,J)
      A(1,1)=4.
      A(1,3)=2.
      A(1,6)=-1.
      A(2,2)=-7.
      A(2,5)=-1.
      A(2,6)=2.
      A(3,1)=1.
      A(3,3)=4.
      A(3,4)=-1.
      A(4,2)=1.
      A(4,4)=2.
      A(5,1)=-1.
      A(5,5)=3.
      A(6,2)=-1.
      A(6,4)=-1.
      A(6,6)=2.
      B(1)=4.
      B(2)=-7.
      B(3)=9.
      B(4)=10.
      B(5)=14.
      B(6)=6.
C  SET THE ERROR CRITERIA AND THE MAXIMUM NUMBER OF ITERATIONS
      ERLIM=1.E-5
      ITMAX=100
C
C  CALL GAUSS-SEIDEL METHOD
C
      CALL GAUSID(N,A,B,X,ERLIM,ITMAX)
C
C  PRINT OUT RESULTS
C
      WRITE(6,88)
   88 FORMAT( ///)
      DO 110 I=1,N
  110 WRITE(6,22)I,X(I)
      WRITE(6,88)
   22 FORMAT( 10X,3H X(,I2,3H)= ,D14.3)
      STOP
      END
C
C**************************  ABSTRACT  **********************************
C
C     THIS SUBROUTINE SOLVES A SET OF LINEAR EQUATIONS USING THE GAUSS-
C  SEIDEL METHOD.
C
C**************************  NOMENCLATURE  ******************************
C
C  A(I,J)-  THE COEFFICIENT OF THE JTH VARIABLE IN THE ITH EQUATION
C  B(I)-  THE CONSTANT TERM FOR THE ITH EQUATION
C  DIA-  THE VALUE OF THE DIAGONAL ELEMENT
C  DXMAX- MAXIMUM ERROR
C  ERLIM- THE ERROR CRITERIA
C  ITER-  THE ITERATION COUNTER
C  ITMAX- THE MAXIMUM NUMBER OF ITERATIONS
C  N-  THE NUMBER OF LINEAR EQUATIONS
C  X(I)-  THE VECTOR CONTAINING THE SOLUTION
C
C************************************************************************
C
C
      SUBROUTINE GAUSID(N,A,B,X,ERLIM,ITMAX)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION A(N,1),B(1),X(1)
C
C  NORMALIZE EACH EQUATION WITH ITS DIAGONAL ELEMENT
C
      DO 1 I=1,N
      DIA=A(I,I)
      B(I)=B(I)/DIA
      DO 2 J=1,N
    2 A(I,J)=A(I,J)/DIA
    1 CONTINUE
C
C  BEGIN THE ITERATION PROCESS
C
      ITER=0
  100 ITER=ITER+1
      DXMAX=0.0
      DO 3 I=1,N
      DIA=X(I)
      X(I)=B(I)
C  APPLY THE GAUSS-SEIDEL APPROXIMATION TO EACH EQUATION
      DO 4 J=1,N
    4 IF(J.NE.I)X(I)=X(I)-A(I,J)*X(J)
    3 IF(DABS(X(I)-DIA).GT.DXMAX)DXMAX=DABS(X(I)-DIA)
      IF(ITER.GT.ITMAX)GO TO 5
C
C  CHECH FOR CONVERGENCE
C
      IF(DXMAX.GT.ERLIM)GO TO 100
C
C  PRINT OUT NUMBER OF ITERATIONS AND DXMAX
C
      WRITE(6,81)
   81 FORMAT( 10X,30H GAUSS-SEIDEL METHOD CONVERGED)
      WRITE(6,55)ITER,DXMAX
   55 FORMAT( 10X,6H ITER=,I6,5X,4H DX=,D14.7)
      RETURN
    5 WRITE(6,6)DXMAX
    6 FORMAT( 44H GAUSS-SEIDEL METHOD DID NOT CONVERGE DXMAX=,D14.7)
      RETURN
      END
