C
C*****************************  ABSTRACT  *******************************
C
C     THIS PROGRAM CALL SUBROUTINE MODFIB IN ORDER TO FIND THE MAXIMUM OF
C  A FUNCTION.	SUBROUTINE MODIFB USES THE MODIFIED FIBONACCI METHOD TO
C  FIND THE OPTIMUM OF A BOUNDED ONE DIMENSIONAL OPTIMIZATION PROBLEM TO
C  A RESOLUTION XMIN.
C
C***************************  NOMENCLATURE  *****************************
C
C  XMIN-  THE DESIRED ACCURACY IN THE OPTIMUM VALUE OF X
C  TYPE-  +1 PROGRAM SEEKS A MAXIMUM; -1 PROGRAM SEEKS A MINIMUM
C  XLB-  THE LEFT BOUNDARY OF THE REGION OF UNCERTAINTY
C  XRL-  THE RIGHT BPUNDARY OF THE REGION OF UNCERTAINTY
C  YLB-  VALUE OF Y AT X=XLB
C  YRB-  VALUE OF Y AT X=XRB
C
C************************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
      EXTERNAL F
C  INPUT INITIAL PROBLEM PARAMETERS
      TYPE=1.
      XMIN=1.E-3
      XLB=-10.
      XRB=10.
C
C  CALL MODFIB
C
      CALL MODFIB(F,XLB,XRB,XMIN,TYPE,YLB,YRB)
C
C  PRINT OUT RESULTS
C
   10 WRITE(6,20)XLB,XRB
   20 FORMAT( 26H THE OPTIMUN X IS BETWEEN ,D13.6,4H AND,D13.6)
   30 FORMAT( 26H THE OPTIMUM Y IS BETWEEN ,D13.6,4H AND,D13.6)
      WRITE(6,30)YLB,YRB
      STOP
      END
C
C*****************************  ABSTRACT  *******************************
C
C     THIS SUBROUTINE CALCULATES THE OPTIMUM OF A BOUNDED, ONE-DIMENSIONAL
C  OBJECTIVE FUNCTION USING THE MODIFIED FIBONACCI METHOD.  WHEN TYPE
C  IS EQUAL TO +1, THE PROGRAM SEEKS A MAXIMUM.  WHEN TYPE IS EQUAL TO
C  -1, THE PROGRAM SEEKS A MINIMUM.
C
C***************************  NOMENCLATURE  *****************************
C
C  XL-	THE LENGTH OF THE REGION OF UNCERTAINTY
C  XMIN-  THE DESIRED ACCURACY IN THE OPTIMUM VALUE OF X
C  TYPE-  +1 PROGRAM SEEKS A MAXIMUM; -1 PROGRAM SEEKS A MINIMUM
C  XLB-  THE LEFT BOUNDARY OF THE REGION OF UNCERTAINTY
C  XRL-  THE RIGHT BPUNDARY OF THE REGION OF UNCERTAINTY
C  X1-  VALUE OF X AT (XLB+.382*L)
C  X2-  VALUE OF X AT (XRB-.382*L)
C  YLB-  VALUE OF Y AT X=XLB
C  YRB-  VALUE OF Y AT X=XRB
C  Y1-  VALUE OF Y AT X=X1
C  Y2-  VALUE OF Y AT X=X2
C
C************************************************************************
      SUBROUTINE MODFIB(F,XLB,XRB,XMIN,TYPE,YLB,YRB)
      IMPLICIT REAL*8(A-H,O-Z)
      EXTERNAL F
C  EVALUATE Y AT THE BOUNDARIES
      CALL F(XLB,YLB)
      CALL F(XRB,YRB)
C  INITIATE THE MODIFIED FIBONACCI SEARCH
      XL=XRB-XLB
      X1=XLB+.382*XL
      CALL F(X1,Y1)
      X2=XRB-.382*XL
      CALL F(X2,Y2)
    1 CONTINUE
      IF(TYPE*Y2.GT.TYPE*Y1)GO TO 2
C  DISCARD THE RIGHT SIDE OF THE REGION OF UNCERTAINTY
      XRB=X2
      YRB=Y2
      X2=X1
      Y2=Y1
      XL=XRB-XLB
      IF(XL.LT.XMIN) GO TO 10
      X1=XLB+.382*XL
      CALL F(X1,Y1)
      GO TO 1
C  DISCARD THE LEFT SIDE OF THE REGION OF UNCERTAINTY
    2 XLB=X1
      YLB=Y1
      X1=X2
      Y1=Y2
      XL=XRB-XLB
      IF(XL.LT.XMIN)GO TO 10
      X2=XRB-.382*XL
      CALL F(X2,Y2)
      GO TO 1
C  RETURN TO CALLING PROGRAM
  10  RETURN
      END
C
C
C****************************  ABSTRACT  ********************************
C
C    THIS SUBROUTINE CALCULATES THE VALUE OF THE OBJECTIVE FUNCTION Y
C  GIVEN THE VALUE OF X.
C
C************************************************************************
C
      SUBROUTINE F(X,FV)
      IMPLICIT REAL*8(A-H,O-Z)
      FV=-3.0*X*X+10.*X+5.
      RETURN
      END
