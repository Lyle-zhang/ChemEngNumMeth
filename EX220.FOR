C
C*****************************	ABSTRACT  *******************************
C
C     THIS PROGRAM APPLIES THE ILLINOIS METHOD FOR THE SOLUTION OF A
C  EXAMPLE 2.20 .
C
C****************************  NOMENCLATURE  ****************************
C
C  AE1- THE ACTIVATION ENERGY FOR THE FIRST REACTION (CAL/GMOLE)
C  AE2- THE ACTIVATION ENERGY FOR THE SECOND REACTION (CAL/GMOLE)
C  CA- THE CONCENTRATION OF A IN THE REACTOR (GMOLES/L)
C  CAO- THE CONCENTRATION OF A IN REACTOR FEED (GMOLE/L)
C  CBA- THE CONCENTRATION OF B IN THE REACTOR (GMOLES/L)
C  CP- THE HEAT CAPACITY OF THE REACTION MIXTURE (CAL/G-DEG C)
C  DEN- DENSITY OF THE REACTION MIXTURE (G/CC)
C  DH1- HEAT OF REACION FOR THE FIRST REACTION (CAL/GMOLE)
C  DH2- HEAT OF REACION FOR THE SECOND REACTION (CAL/GMOLE)
C  ERLIM- CONVERGENCE CRITERIA
C  ICMAX- THE MAXIMUM NUMBER OF ITERATIONS ALLOWED
C  IPRINT- PRINT FLAG =1 PRINT INTERMEDIATE VALUES; =0 NO PRINT
C  K1- REACTION RATE CONSTANT FOR THE FIRST REACTION (1/SEC)
C  K2- REACTION RATE CONSTANT FOR THE SECOND REACTION (1/SEC)
C  Q- VOLUMETRIC FEED RATE TO THE REACTOR (L/SEC)
C  R- GAS CONSTANT (1.987 CAL/GMOLE-DEG C)
C  TIN- TEMPERATURE OF THE FEED TO THE REACTOR (DEG C)
C  VR- VOLUME OF THE REACTOR (L)
C  X-  THE INITIAL GUESS FOR THE ROOT (INPUT); THE SOLUTION VALUE OF
C      X (OUTPUT)
C
C************************************************************************
C
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 K1,K2
      COMMON /DATA1/AE1,AE2,R,CAO,Q,VR,K1,K2,DEN,CP,TIN,DH1,DH2,CA,CB
C  SPECIFY NUMERICAL PARAMETERS
      ERLIM=1.E-3
      ICMAX=100
      IPRINT=1
C  SPECIFY REACTOR PARAMETERS
      AE1=10000.
      AE2=15000.
      R=1.987
      CAO=1.
      Q=50.
      VR=100.
      K1=1.8E6
      K2=3.84E8
      DEN=.8
      CP=.8
      TIN=10.
      DH1=-50000.
      DH2=-40000.
C  SPECIFY STARTING POINT
      X=50.
C
C  CALL NEWT1D
C
      CALL ILLIN(ERLIM,ICMAX,IPRINT,X)
C
C  WRITE FINAL ANSWERS
C
      WRITE(6,33)
   33 FORMAT( /)
      WRITE(6,44)X,CA,CB
   44 FORMAT( 5X,' CONVERGED ANSWERS ARE   T=',F6.2,'   CA=',F6.4,
     1 '   CB=',F6.4)
C
C  END
C
      STOP
      END
C****************  ABSTRACT  **********************************
C
C   THIS SUBROUTINE EMPLOYS THE ILLINOIS METHOD FOR SOLUTION
C   OF A SINGLE NON LINEAR EQUATION IN ONE UNKNOWN.
C   ILLINOIS METHOD IS A MODIFICATION OF THE REGULA FALSI
C   METHOD AND IT DEMONSTRATES FASTER CONVERGENCE FOR CONVEX
C   FUNCTIONS. IT IS APPLIED ONLY WHEN THE FUNCTION APPROACHES
C   THE SOLUTION IN CONVEX FASHION, OTHERWISE THE PROGRAM 
C   PROCEEDS USING THE NORMAL REGULA FALSI METHOD.
C
C   IT IS NECESSARY TO INPUT ONLY ONE GUESS VALUE, AND THE 
C   FUNCTION EQUATION IS CONTAINED IN THE SUBROUTINE 'F'.
C
C****************  NOMENCLATURE  *******************************
C
C   XG- INITIAL GUESS VALUE SUPPLIED AS INPUT
C   ERLIM- ERROR CRITERIA FOR CONVERGENCE
C   X1INT- INTERMEDIATE VALUE FOR BOUNDING THE FUNCTION
C   X2INT- VALUE FOUND FOR BOUNDING THE FUNCTION
C   XMULT- MULTIPLIER USED ON X1INT IN SEARCHING FOR X2INT
C   X1OLD- FIRST WORK VARIABLE FOR CALCULATING THE NEW VALUE
C   X2OLD- SECOND WORK VARIABLE FOR CALCULATING THE NEW VALUE
C   X1NEW- NEW VALUE CALCULATED USING THE REGULA FALSI METHOD
C   X2NEW- NEXT VALUE CALCULATED USING ILLINOIS MODIFICATION
C   ERR1- FUNCTION VALUE FOR VARIABLE X1OLD
C   ERR2- FUNCTION VALUE FOR VARIABLE X2OLD
C   E1NEW- FUNCTION VALUE FOR VARIABLE X1NEW
C   E2NEW- FUNCTION VALUE FOR VARIABLE X2NEW
C   AI- ALFA FACTOR FOR ILLINOIS METHOD
C   TEST- TEST VARIABLE
C   ANSWER- THE FINAL CONVERGED VALUE
C
C*****************************************************************
C
      SUBROUTINE ILLIN(ERLIM,ICMAX,IPRINT,XG)
      IMPLICIT REAL*8(A-H,O-Z)
      IC=0
      X1OLD=XG
      CALL F(X1OLD,ERR1)
      X1INT=X1OLD
      IF(X1INT.EQ.0.0)X1INT=1.0
      
C
C  FIND OTHER GUESS VALUE TO BOUND THE FUNCTION 
C
      XMULT=1.1
  10  X2INT=X1INT*XMULT
      CALL F(X2INT,ERR2)
      TEST=ERR1*ERR2
      IF(TEST.LT.0.0)GO TO 100
      X2INT=X1INT/XMULT
      CALL F(X2INT,ERR2)
      TEST=ERR1*ERR2
      IF(TEST.LT.0.0)GO TO 100
      XMULT=XMULT+0.5
      GO TO 10
C
C  BEGIN THE REGULA FALSI METHOD
C -------------------------------
C
 100  X2OLD=X2INT
      AI=0.5
C
  20  CALL F(X1OLD,ERR1)
      CALL F(X2OLD,ERR2)
C
C  FORMULA FOR REGULA FALSI METHOD
C
      X1NEW=(ERR2/(ERR2-ERR1))*X1OLD+(ERR1/(ERR1-ERR2))*X2OLD
C
      CALL F(X1NEW,E1NEW)
      IC=IC+1
C  CHECK FOR EXCESSIVE ITERATIONS
      IF(IC.GT.ICMAX)WRITE(6,22)
   22 FORMAT( ' ILLINOIS METHOD DID NOT CONVERGE')
      IF(IC.GT.ICMAX)STOP
C  PRINT OUT INTERMEDIATE RESULTS
      IF(IPRINT.EQ.1)WRITE(6,11)IC,X1NEW,E1NEW
   11 FORMAT( 5X,' IC=',I3,5X,' X=',F7.2,5X,' F(X)=',D10.3)
C
      XTEST=X1NEW-X1SAV
      IF(DABS(XTEST).LT.ERLIM)GO TO 400
      X1SAV=X1NEW
C
C  CHECK IF ILLINOIS MODIFICATION IS APPLICABLE
C ----------------------------------------------
C
      TEST=ERR2*E1NEW
      IF(TEST.GT.0.0)GO TO 200
C
C  IF NOT, CONTINUE WITH REGULA FALSI
C
      X1OLD=X2OLD
      X2OLD=X1NEW
      GO TO 20
C
C  APPLICATION OF THE ILLINOIS METHOD
C ------------------------------------
C
 200  X2NEW=((AI*ERR1)/(AI*ERR1-E1NEW))*X1NEW+
     $          (E1NEW/(E1NEW-AI*ERR1))*X1OLD
C
      CALL F(X2NEW,E2NEW)
      IF(DABS(E2NEW).LT.ERLIM)GO TO 500
C
      TEST=E2NEW*E1NEW
      IF(TEST.LT.0.0)GO TO 300
      X2OLD=X2NEW
      GO TO 20
C
 300  X1OLD=X1NEW
      X2OLD=X2NEW
      GO TO 20
C
C  WRITE THE FINAL ANSWER
C
 400  ANSWER=X1NEW
      GO TO 600
 500  ANSWER=X2NEW
 600  XG=ANSWER
      RETURN
      END
C
C*****************************  ABSTRACT  *******************************
C
C     THIS SUBROUTINE CALCULATES THE CALCULATES THE VALUE OF F(X)
C  GIVEN THE VALUE OF X.
C
C************************************************************************
C
      SUBROUTINE F(X,FV)
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 K1,K2
      COMMON /DATA1/AE1,AE2,R,CAO,Q,VR,K1,K2,DEN,CP,TIN,DH1,DH2,CA,CB
      E1=DEXP(-AE1/R/(X+273.15))
      E2=DEXP(-AE2/R/(X+273.15))
      CA=CAO*Q/(Q+VR*K1*E1)
      CB=VR*CA*K1*E1/(Q+VR*K2*E2)
      FV=DEN*CP*Q*1000.*(X-TIN)+VR*(K1*CA*E1*DH1+K2*CB*E2*DH2)
      RETURN
      END
