C***********************   ABSTRACT   *********************************
C
C     THIS PROGRAM CALCULATES THE PERFORMANCE OF AN ISOTHERMAL CSTR
C   REACTOR. USING A STEADY STATE MOLE BALANCE FOR EACH REACTION SPECIES,
C   A SYSTEM OF FOUR NONLINEAR EQUATIONS CONTAINING FOUR UNKNOWNS IS
C   GENERATED.  THIS SYSTEM OF EQUATIONS IS SOLVED USING THE NEWTON
C   METHOD.
C
C***********************   NOMENCLATURE   *******************************
C
C  CAO-  THE INITAL CONCENTRATION OF A IN THE FEED STREAM (GMOLES/L)
C  ERLIM-  ERROR CRITERIA
C  FX(I)-  THE VALUE OF THE NONLINEAR EQUATIONS WHERE I INDICATES
C          WHICH EQUATION
C  KI-  THE RATE CONSTANT FOR EACH OF THE REACTIONS (I INDICATES THE
C       THE REACTION NUMBER)
C  N-  THE NUMBER OF NONLINEAR EQUATIONS
C  Q-  THE VOLUMETRIC FLOW RATE INTO AND OUT OF THE REACTOR (L/SEC)
C  X(I)-  THE INDEPENDENT VARIABLES OF THE PROBLEM  I=1 (CONC OF A),
C         I=2 (CONC OF B), I=3 (CONC OF C), AND I=4 (CONC OF D)
C         ALL CONC ARE GIVEN IN GMOLES/L
C
C
C*****************   INPUT DESCRIPTION   ********************
C
C     THE INITIAL GUESSES ARE SPECIFIED  IN THE MAIN PROGRAM AS WELL
C  AS THE ERROR CRITERIA AND THE NUMBER OF NONLINEAR EQUATIONS.
C  THE FUNCTIONS ARE SPECIFIED IN SUBROUTINE FUNC AND THE PARTIAL
C  DERIVATIVES OF THE FUNCTION WITH RESPECT TO THE INDEPENDENT
C  VARIABLES ARE SPECIFIED IN SUBROUTINE DER.
C
C
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION X(4),FX(4),Y(4),A(4,4),B(4),D(4),IPVT(4)
      COMMON /ONE/ K1,K2,K3,K4,VR,Q,CAO
C  SPECIFY EXTERNAL STATEMENT
      EXTERNAL FUNC,DER
      REAL K1,K2,K3,K4
C
C  INPUT THE RATE CONSTANTS
C
      K1=1.
      K2=.2
      K3=.05
      K4=.4
C
C  INPUT REACTOR VOLUME, FLOW RATE, AND INLET CONCENTRATION
C
      VR=100.
      Q=50.
      CAO=1.
C
C  MAKE INITIAL GUESSES
C
      X(1)=.3
      X(2)=.3
      X(3)=.3
      X(4)=.3
      N=4
      ERLIM=1.E-3
C
C  CALL NEWTON METHOD
C
      CALL NEWTNA(FUNC,DER,N,X,FX,ERLIM,A,B,D,IPVT)
C
C  PRINT OUT FINAL RESULTS
C
      DO 6 I=1,4
    6 WRITE(6,7)I,X(I)
    7 FORMAT( 10X,3H I=,I3,5X,3H X=,D14.7)
      STOP
      END
C
C*************************   ABSTRACT  **********************************
C
C     THIS SUBROUTINE CALCULATES THE PARTIAL DERIVATIVES OF THE
C  THE FUNCTIONS WITH RESPECT TO THE INDEPENDENT VARIABLES.
C  A(I,J) REPRESENTS THE PARTIAL OF THE ITH FUNCTION WITH RESPECT
C  TO THE JTH VARIABLE.
C
C*************************  INPUT DESCRIPTION  **************************
C
C      THE VALUE OF N AND X(I) ARE SUPPLIED TO SUBROUTINE DER BY SUBROUTINE
C  NNEWTN THROUGH THE CALLING STATEMENT.
C
C************************************************************************
C
C
      SUBROUTINE DER(N,X,A)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION A(4,4),X(4)
      REAL K1,K2,K3,K4
      COMMON /ONE/K1,K2,K3,K4,VR,Q,CAO
      DO 1 I=1,N
      DO 1 J=1,N
    1 A(I,J)=0.0
      A(1,1)=-1.-VR*K1/Q-1.5*K2*VR*X(1)**.5/Q
      A(1,3)=VR*2.*K3*X(3)/Q
      A(2,1)=2.*VR*K1/Q
      A(2,2)=-1-2.*K4*X(2)/Q*VR
      A(3,1)=1.5*VR*K2*X(1)**.5/Q
      A(3,2)=2.*K4*X(2)/Q*VR
      A(3,3)=-1.-2.*K3*X(3)/Q*VR
      A(4,2)=2.*VR*K4*X(2)/Q
      A(4,4)=-1.
      RETURN
      END
C
C****************************  ABSTRACT  ********************************
C
C     THIS SUBROUTINE CALCULATES THE VALUES OF EACH NONLINEAR
C  EQUATION GIVEN THE VALUE OF X(I) AND N.  THESE VALUES ARE
C  SUPPLIED TO THIS SUBROUTINE WHEN IT IS CALLED BY SUBROUTINE
C  NNEWTN.
C
C************************************************************************
C
      SUBROUTINE FUNC(N,X,FX)
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON /ONE/K1,K2,K3,K4,VR,Q,CAO
      DIMENSION X(4),FX(4)
      REAL K1,K2,K3,K4
      FX(1)=CAO  -X(1)+VR*(-K1*X(1)+K3*X(3)*X(3)-K2*X(1)**1.5)/Q
      FX(2)=-X(2)+VR*(2.*K1*X(1)-K4*X(2)*X(2))/Q
      FX(3)=-X(3)+VR*(K2*X(1)**1.5-K3*X(3)*X(3)+K4*X(2)*X(2))/Q
      FX(4)=-X(4)+VR*(K4*X(2)*X(2))/Q
      WRITE(6,11)(X(I),FX(I),I=1,N)
   11 FORMAT( 10X,' D=',E14.7,5X,' F=',E14.7)
      WRITE(6,22)
   22 FORMAT( /)
      RETURN
      END
C
C****************************  ABSTRACT   *******************************
C
C      THIS SUBROUTINE EMPLOYES NEWTON'S METHOD IN ORDER TO SOLVE A
C  SET OF N NONLINEAR EQUATIONS CONTAINING N UNKNOWNS.
C  THIS SUBROUTINE IS CALLED BY THE MAIN PROGRAM AND IS SUPPLIED
C  THE VALUES OF THE INITIAL GUESS FOR X(I)'S AS WELL AS THE VALUE OF
C  N.  THIS SUBROUTINE USES THE VALUES OF THE FUNCTION FROM FUNC AND
C  THE VALUES OF THE PARTIAL DERIVATIVES OF THE FUNCTION IN ORDER TO
C  DETERMINE THE SOLUTION. THIS METHOD USES THE LIBRARY ROUTINE LINPAC
C  TO SOLVE THE SYSTEM OF LINEAR EQUATION USED BY NEWTON'S METHOD.
C
C************************************************************************
C
C
      SUBROUTINE NEWTNA(FUNC,DER,N,X,FX,ERLIM,A,B,D,IPVT)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION A(N,1),B(1),D(1),IPVT(1),X(1),FX(1)
      EXTERNAL FUNC,DER
    1 CONTINUE
      ITEST=0
C
C  MAKE FUNCTION EVALUATIONS
C
      CALL FUNC(N,X,FX)
      DO 3 I=1,N
    3 B(I)=-FX(I)
C
C  EVALUATE JACOBIAN MATRIX
C
      CALL DER(N,X,A)
C
C  CALL LINEAR EQUATION SOLVER
C
      CALL LINPAC(N,A,B,D,IPVT)
C
C  MAKE AN IMPROVED VALUE FOR X(I)
C
      ITEST=0
      DO 5 I=1,N
C  ANTICIPATE ZERO ROOTS
      TS=DABS(X(I))
      IF(TS.LT.1.D-10)GO TO 5
      RAT=D(I)/X(I)
      IF(DABS(RAT).GT.ERLIM)ITEST=ITEST+1
    5 X(I)=X(I)+D(I)
C
C  CHECK FOR CONVERGENCE
C
      IF(ITEST.NE.0)GO TO 1
      RETURN
      END
